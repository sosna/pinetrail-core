plugins {
  id 'java-library'
  id 'com.diffplug.spotless' version '5.6.1'
  id 'com.github.spotbugs' version '4.5.0'
  id 'jacoco'
}

repositories {
  jcenter()
}

dependencies {
  implementation 'org.slf4j:slf4j-api:1.7.30'
  implementation 'org.hibernate:hibernate-validator:5.4.3.Final'
  runtimeOnly 'org.glassfish.web:javax.el:2.2.6'
  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
  testImplementation 'nl.jqno.equalsverifier:equalsverifier:3.4.2'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
}

version = '1.0.0-SNAPSHOT'

sourceCompatibility = '11'
targetCompatibility = '11'

test {
  useJUnitPlatform()
  finalizedBy jacocoTestReport 
}

jacocoTestReport {
  reports {
    xml.enabled true
  }
  dependsOn test
}

jacocoTestCoverageVerification {
  violationRules {
    rule {
      limit {
        counter = 'CLASS'
        value = 'COVEREDRATIO'
        minimum = 1.0
      }
      limit {
        counter = 'METHOD'
        value = 'COVEREDRATIO'
        minimum = 1.0
      }
      limit {
        counter = 'LINE'
        value = 'COVEREDRATIO'
        minimum = 1.0
      }
      limit {
        counter = 'BRANCH'
        value = 'COVEREDRATIO'
        minimum = 0.9
      }
      limit {
        counter = 'INSTRUCTION'
        value = 'COVEREDRATIO'
        minimum = 1.0
      }
    }
  }
}
check.dependsOn jacocoTestCoverageVerification

spotbugsMain {
  reports {
    html {
      enabled = true
      destination = file("$buildDir/reports/spotbugs/main/spotbugs.html")
      stylesheet = 'fancy-hist.xsl'
    }
  }
}

spotless {
  java {
    googleJavaFormat()
  }
}

jar {
  manifest {
    attributes('Implementation-Title': project.name,
               'Implementation-Version': project.version)
  }
}

java {
  //withJavadocJar()
  withSourcesJar()
}
